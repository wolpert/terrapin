/*
 * Copyright (c) 2020 CodeHead Systems. All rights reserved
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.


// Testing in Intelij requires the library in your lib path. Errors will show where it needs to be. Example
// from an Ubuntu box
//  sudo mkdir /usr/lib/jni
//  sudo cp -i oop-mock-dynamodb/build/libs/libsqlite4java-linux-amd64-1.0.392.so /usr/lib/jni
buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://maven.google.com" } // Google's Maven repository
        gradlePluginPortal() // was jcenter() which is dying
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath 'com.google.gms:google-services:4.3.3'
        classpath 'org.gretty:gretty:3.0.2'
        classpath 'io.github.gradle-nexus:publish-plugin:1.1.0'
    }
}

apply plugin: 'io.github.gradle-nexus.publish-plugin'
nexusPublishing {
    repositories {
        sonatype()
    }
}
// gradle :<project>:bintrayUpload

allprojects {
    apply plugin: "java-library"
    apply plugin: "eclipse"
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()  // was jcenter() which is dying
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://s3-us-west-2.amazonaws.com/dynamodb-local/release" }
    }
    test {
        useJUnitPlatform() // needed for junit5
        finalizedBy jacocoTestReport
    }
    checkstyleTest { // ignore checkstyle in tests
        ignoreFailures true
        showViolations false
    }
    jacocoTestReport {
        dependsOn test
        reports {
            xml.enabled true
            csv.enabled true
        }
        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        '**/dagger/**.class',
                        '**/Dagger*Builder.class',
                        '**/*Exception.class',
                        '**/*Module.class',
                        '**/*_Factory.class',
                        '**/*_Factory.*class',
                        '**/*InstanceHolder.class',
                        '**/*_Factory$InstanceHolder.class'
                ])
            }))
        }
    }
    def excludeFiles = [
            '**.dagger.**',
            '**.Dagger*Builder',
            '**.*Exception',
            '**.*Module',
            '**.*_Factory',
            '**.*_Factory*',
            '**.InstanceHolder',
            '**.*_Factory$InstanceHolder'
    ]

// If you want to override, use this in your package and set. This is the low bar.
    jacocoTestCoverageVerification {
        dependsOn jacocoTestReport
        violationRules {
            rule {
                excludes = excludeFiles
                element = 'CLASS'
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.8
                }
            }
            rule {
                excludes = excludeFiles
                element = 'CLASS'
                limit {
                    counter = 'BRANCH'
                    value = 'COVEREDRATIO'
                    minimum = 0.7
                }
            }
        }
    }
}

group 'com.codeheadsystems.terrapin'
version '1.0.0-SNAPSHOT'

ext {
    codeheadTestVersion = '1.0.1'
    codeheadDatabaseTestVersion = '1.0.0'

    awsLocalDDB = "1.15.0" // https://mvnrepository.com/artifact/com.amazonaws/DynamoDBLocal
    awsSDK = "1.12.313" // https://mvnrepository.com/artifact/com.amazonaws/aws-java-sdk-dynamodb
    awsSDK2 = "2.17.284" // https://mvnrepository.com/artifact/software.amazon.awssdk/dynamodb
    assertjVersion = '3.23.1' // https://mvnrepository.com/artifact/org.assertj/assertj-core
    bouncyVersion = '1.70' // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk15on
    box2DLightsVersion = '1.5' // https://mvnrepository.com/artifact/com.badlogicgames.box2dlights/box2dlights
    cassandraDriverVersion = '4.15.0' //  https://mvnrepository.com/artifact/com.datastax.oss/java-driver-core
    commonsIoVersion = '2.11.0' // https://mvnrepository.com/artifact/commons-io/commons-io
    commonsLangVersion = '3.12.0' // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    daggerVersion = '2.44' // https://mvnrepository.com/artifact/com.google.dagger/dagger
    dropwizardVersion = '2.1.2' // https://mvnrepository.com/artifact/io.dropwizard/dropwizard-core
    guavaVersion = '31.1-jre' // https://mvnrepository.com/artifact/com.google.guava/guava
    immutablesVersion = '2.9.2' // https://mvnrepository.com/artifact/org.immutables/value
    jacksonVersion = '2.13.4' // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
    jacksonDatabindVersion = '2.13.4' // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    jakartaServletVersion = '6.0.0' // https://mvnrepository.com/artifact/jakarta.servlet/jakarta.servlet-api
    javaxInjectVersion = '1'
    javaxServletVersion = '4.0.1' // https://mvnrepository.com/artifact/javax.servlet/javax.servlet-api
    jupiterVersion = '5.9.1' // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    log4jVersion = '2.19.0' //  https://mvnrepository.com/artifact/org.apache.logging.log4j/log4j-core
    logbackVersion = '1.4.1' // https://mvnrepository.com/artifact/ch.qos.logback/logback-core
    metricsVersion = '4.2.12' // https://mvnrepository.com/artifact/io.dropwizard.metrics/metrics-core
    micrometerVersion = '1.9.4' // https://mvnrepository.com/artifact/io.micrometer/micrometer-core
    mockitoVersion = '4.8.0' // https://mvnrepository.com/artifact/org.mockito/mockito-core
    nettyVersion = '4.1.82.Final' //  https://mvnrepository.com/artifact/io.netty/netty-all
    openfeign = '11.10' // https://mvnrepository.com/artifact/io.github.openfeign/feign-core
    retrofitVersion = '2.9.0' // https://mvnrepository.com/artifact/com.squareup.retrofit2/retrofit
    resilience4jVersion = '1.7.1' // https://mvnrepository.com/artifact/io.github.resilience4j/resilience4j-retry
    slf4jVersion = '2.0.3' // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    testContainerVersion = "1.17.3" // https://mvnrepository.com/artifact/org.testcontainers/testcontainers
}

