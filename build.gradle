/*
 * Copyright (c) 2020 CodeHead Systems. All rights reserved
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://maven.google.com" } // Google's Maven repository
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath 'com.google.gms:google-services:4.3.3'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.5"
}

apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'maven-publish'

// gradle :<project>:bintrayUpload

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }

    jar {
        manifest.attributes["Implementation-Title"] = project.name
        manifest.attributes["Implementation-Version"] = project.version
        manifest.attributes["Automatic-Module-Name"] = project.name.replace('-', '.')  // for Jigsaw
        manifest.attributes["Created-By"] =
                "${System.getProperty("java.version")} (${System.getProperty("java.specification.vendor")})"

        from(rootDir) {
            include "README.md"
            include "NOTICE.md"
            include "LICENSE"
            into "META-INF"
            expand(copyright: new Date().format("yyyy"), version: project.version)
        }
    }

    normalization {
        runtimeClasspath {
            ignore "META-INF/MANIFEST.MF"
        }
    }

    javadoc {
        description = "Generates project-level javadoc for use in -javadoc jar"

        options.encoding = "UTF-8"
        options.memberLevel = JavadocMemberLevel.PROTECTED
        options.author = true
        options.header = project.name
        options.use = true
        options.addStringOption("Xdoclint:none", "-quiet")

        // Suppress warnings due to cross-module @see and @link references.
        // Note that global 'api' task does display all warnings.
        logging.captureStandardError LogLevel.INFO
        logging.captureStandardOutput LogLevel.INFO  // suppress "## warnings" message
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        archiveClassifier.set("sources")
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar) {
        archiveClassifier.set("javadoc")
        from javadoc
    }
}

group 'com.codeheadsystems.terrapin'
version '1.0-SNAPSHOT'

ext {
    aiVersion = '1.8.2' // https://mvnrepository.com/artifact/com.badlogicgames.gdx/gdx-ai
    ashleyVersion = '1.7.4' // https://mvnrepository.com/artifact/com.badlogicgames.ashley/ashley
    assertjVersion = '3.19.0' // https://mvnrepository.com/artifact/org.assertj/assertj-core
    bouncyVersion = '1.68' // https://mvnrepository.com/artifact/org.bouncycastle/bcprov-jdk15on
    box2DLightsVersion = '1.5' // https://mvnrepository.com/artifact/com.badlogicgames.box2dlights/box2dlights
    cassandraDriverVersion = '3.7.2' // https://mvnrepository.com/artifact/com.datastax.cassandra/cassandra-driver-core and https://mvnrepository.com/artifact/com.datastax.oss/java-driver-core
    cassandraUnitVersion = '3.11.2.0' // https://mvnrepository.com/artifact/org.cassandraunit/cassandra-unit
    commonsIoVersion = '2.8.0' // https://mvnrepository.com/artifact/commons-io/commons-io
    daggerVersion = '2.35' // https://mvnrepository.com/artifact/com.google.dagger/dagger
    dropwizardVersion = '2.0.21' // https://mvnrepository.com/artifact/io.dropwizard/dropwizard-core
    gdxVersion = '1.10' // https://mvnrepository.com/artifact/com.badlogicgames.gdx/gdx
    guavaVersion = '30.1.1-jre' // https://mvnrepository.com/artifact/com.google.guava/guava
    immutablesVersion = '2.8.8' // https://mvnrepository.com/artifact/org.immutables/value
    jacksonVersion = '2.12.3' // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-core
    javaxInjectVersion = '1'
    jupiterVersion = '5.7.1' // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter-api
    logbackVersion = '1.2.3' // https://mvnrepository.com/artifact/ch.qos.logback/logback-core
    metricsVersion = '4.1.20' // https://mvnrepository.com/artifact/io.dropwizard.metrics/metrics-core
    mockitoVersion = '3.9.0' // https://mvnrepository.com/artifact/org.mockito/mockito-core
    openfeign = '11.1' // https://mvnrepository.com/artifact/io.github.openfeign/feign-core
    slf4jVersion = '1.7.30' // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
}

